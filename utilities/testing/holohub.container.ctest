# SPDX-FileCopyrightText: Copyright (c) 2025 NVIDIA CORPORATION & AFFILIATES. All rights reserved.
# SPDX-License-Identifier: Apache-2.0
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

if(NOT APP)
  message(FATAL_ERROR "A holohub application should be set via the APP variable.")
endif()

if(NOT TAG)
  set(TAG latest)
endif()

set(CTEST_SOURCE_DIRECTORY "/workspace/holohub")
set(CTEST_BINARY_DIRECTORY "/workspace/holohub/build-${APP}")

if(NOT CTEST_SITE)
  set(CTEST_SITE "SITE_NA")
endif()

if(NOT PLATFORM_NAME)
  set(PLATFORM_NAME linux)
endif()

set(CTEST_BUILD_NAME "holohub-${PLATFORM_NAME}-${APP}-${TAG}")

set(CTEST_UPDATE_COMMAND "git")
set(CTEST_CMAKE_GENERATOR "Ninja")

ctest_empty_binary_directory(${CTEST_BINARY_DIRECTORY})

# Copy the CDash configurations
file(COPY
    ${CMAKE_CURRENT_LIST_DIR}/CTestCustom.cmake
    DESTINATION ${CTEST_BINARY_DIRECTORY})
ctest_read_custom_files("${CTEST_BINARY_DIRECTORY}")

set(configure_options
  ${CONFIGURE_OPTIONS}
  -D HOLOHUB_DATA_DIR=/workspace/holohub/data-${APP}-${TAG}
  -D CMAKE_CUDA_COMPILER=/usr/local/cuda/bin/nvcc
  -D CMAKE_BUILD_TYPE=RelWithDebInfo
)

list(APPEND configure_options -DAPP_${APP}=ON)

# Default nightly start time
set(CTEST_NIGHTLY_START_TIME "06:00:00 UTC")

ctest_start(Nightly)
ctest_update()
ctest_configure(OPTIONS "${configure_options}")
ctest_build()
ctest_test()

if(CTEST_SUBMIT_URL)
  ctest_submit()
endif()
